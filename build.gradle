defaultTasks 'build'

apply plugin: 'java'

configurations {
	xjc
}

xsdOutputDir = "$buildDir/xsd/generated-sources"
xsdInputDir = "src/main/xsd"

task xsdToJava {
	outputs.dir xsdOutputDir
	inputs.dir xsdInputDir
	doLast {
		ant.taskdef (
			name: "xjc",
			classname: "com.sun.tools.xjc.XJCTask",
			classpath: configurations.xjc.asPath
		)
		ant.mkdir(dir: xsdOutputDir)
		ant.xjc (
			destDir: xsdOutputDir,
			package: "net.northfuse.ofx4j.domain",
			schema: "$xsdInputDir/OFX2_Protocol.xsd"
		)
	}
}

dependencies {
	xjc "sun-jaxb:jaxb-xjc:2.1.9"
	xjc "javax.xml.bind:jaxb-api:2.1"
	xjc "sun-jaxb:jaxb-impl:2.1.9"
}

repositories {
	mavenCentral()
	mavenRepo urls: ["http://repository.jboss.org/maven2","http://download.java.net/maven/2"]
}

sourceSets {
	main {
		java {
			srcDirs += xsdOutputDir
		}
	}
}

compileJava.dependsOn xsdToJava
